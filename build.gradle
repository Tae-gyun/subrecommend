plugins {
 //   id 'org.springframework.boot' version '3.1.0' apply false
 //   id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'org.springframework.boot' version '3.2.6' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
    id "org.sonarqube" version "5.0.0.4638" apply false		//apply false 해야 서브 프로젝트에 제대로 적용됨
    id 'java'
}

allprojects {
    group = 'com.subrecommend'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco' // 서브 프로젝트에 JaCoCo 플러그인 적용

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    //-- for spring cloud
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.2"
        }
    }

    jacoco {
        toolVersion = "0.8.8" // 사용할 JaCoCo 버전 지정
    }

    test {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            html.required = true
            html.outputLocation = layout.buildDirectory.dir("jacocoHtml").get().asFile
        }
    }
}